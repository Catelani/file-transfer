configurations {
  fatJar {
    description "Configurações especiais para o Fat Jar"
  }
}

dependencies {
  compile project(':file-transfer-core')

  // para suporte a linha de comando
  compile group: 'commons-cli', name: 'commons-cli'

  // sem logs para o cli
  fatJar 'org.slf4j:slf4j-nop:1.7.20'
}

//create a single Jar with all dependencies
task fatJar(type: Jar, group: 'build') {
  manifest {
    attributes 'Implementation-Title': 'File Transfer CLI',
               'Implementation-Version': "${project.version}",
               'Main-Class': 'br.com.catelani.santander.filetransfer.cli.CliMain'
  }
  baseName = project.name
  from {
    (configurations.compile - configurations.provided + configurations.fatJar).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  with jar
}
